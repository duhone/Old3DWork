vs.1.1

;transform pos
dp4 oPos.x, v0, c2
dp4 oPos.y, v0, c3
dp4 oPos.z, v0, c4
dp4 oPos.w, v0, c5


;light vector
add r0.xyz,c11.xyz,-v0.xyz
dp3 r1,r0,r0
rsq r1,r1
mul r0.xyz,r0.xyz,r1.xyz

;sXt
mov r9.xyz,v4.xyz

mov r10.xyz,v3.xyz

mul r3.xyz,r9.yzx,r10.zxy
mad r3.xyz,-r10.yzx,r9.zxy,r3.xyz
dp3 r5,r3,r3
rsq r5,r5
mul r11.xyz,r5.xyz,r3.xyz

;transform to tangent space
dp3 r4.x,r0.xyz,r9.xyz
dp3 r4.y,r0.xyz,r10.xyz
dp3 r4.z,r0.xyz,r11.xyz
dp3 r1,r4,r4
rsq r1,r1
mad r4.xyz,r4.xyz,r1.xyz,c1.xyz
mul r4.xyz,r4.xyz,c80

;output light vector
mov oD0.xyz,r4.xyz
mov oD0.w,c1.w

;view vector
dp3 r6.x, v0, c6
dp3 r6.y, v0, c7
dp3 r6.zw, v0, c8

;mov r1,c14
add r1.xyz,c14.xyz,-r6.xyz
dp3 r2,r1,r1
rsq r2,r2
mul r1.xyz,r1.xyz,r2.xyz
mov r5.xyz,r1.xyz


add r1.xyz,c13.xyz,-v0.xyz
dp3 r2,r1,r1
rsq r2,r2
mul r1.xyz,r1.xyz,r2.xyz

;half way
add r0.xyz,r0.xyz,r1.xyz
dp3 r1,r0,r0
rsq r1,r1
mul r0.xyz,r1.xyz,r0.xyz

;transform half vec to tangent space
dp3 r4.x,r0.xyz,r9.xyz
dp3 r4.y,r0.xyz,r10.xyz
dp3 r4.z,r0.xyz,r11.xyz
dp3 r1,r4,r4
rsq r1,r1
mad r4.xyz,r4.xyz,r1.xyz,c1.xyz
mul r4.xyz,r4.xyz,c80.xyz

;output half vector
mov oT1.xyz,r4.xyz
mov oT1.w,c1.w




;output view vector
mov oT2.xyz,r5.xyz
mov oT2.w,c1.w

; Rotate the basis vectors by the world transform to put them into world space
dp3 oT3.x, r9,  c6
dp3 oT3.y, r10,  c6
dp3 oT3.z, r11, c6

dp3 oT4.x, r9,  c7
dp3 oT4.y, r10,  c7
dp3 oT4.z, r11, c7

dp3 oT5.x, r9,  c8
dp3 oT5.y, r10,  c8
dp3 oT5.z, r11, c8



;texture
mov oT0.xy,v2.xy

;61 instructions